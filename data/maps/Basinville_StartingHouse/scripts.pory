mapscripts Basinville_StartingHouse_MapScripts {}

const MULTI_HEAL_PARTY = 0
const MULTI_WATER_SHOP = 1
const MULTI_SELL_ITEMS = 2
const MULTI_TANGENT = 3
const MULTI_NOTHING = 4

script Basinville_StartingHouse_Shiori {
    lock
    if (!flag(FLAG_SYS_POKEMON_GET)) {
        call(Basinville_StartingHouse_Shiori_StartingOut)
        release
        end
    }
    setspeakername(Basinville_StartingHouse_Text_ShioriName)
    message(Basinville_StartingHouse_Text_ShioriRegular)
    waitmessage
    while {
        call(Basinville_StartingHouse_Shiori_PrepareMultichoice)
        if (var(VAR_RESULT) > MULTI_TANGENT) {
            break
        }
        switch (var(VAR_RESULT)) {
            case MULTI_HEAL_PARTY:
                call(Basinville_StartingHouse_Shiori_HealParty)
            case MULTI_WATER_SHOP:
                call(Basinville_StartingHouse_Shiori_WaterShop)
            case MULTI_SELL_ITEMS:
                call(Basinville_StartingHouse_Shiori_SellItems)
            case MULTI_TANGENT:
                call(Basinville_StartingHouse_Shiori_Tangent)
        }
        message(Basinville_StartingHouse_Text_ShioriRegularAgain)
        waitmessage
    }
    msgbox(Basinville_StartingHouse_Text_ShioriRegular2)
    hidespeakername
    release
    end
}

script Basinville_StartingHouse_Shiori_PrepareMultichoice {
    dynmultipush(Basinville_StartingHouse_Text_HealParty, MULTI_HEAL_PARTY)
    dynmultipush(Basinville_StartingHouse_Text_WaterShop, MULTI_WATER_SHOP)
    dynmultipush(Basinville_StartingHouse_Text_SellItems, MULTI_SELL_ITEMS)
    dynmultipush(Basinville_StartingHouse_Text_Tangent, MULTI_TANGENT)
    dynmultipush(Basinville_StartingHouse_Text_Nothing, MULTI_NOTHING)
    dynmultistack(0, 0, FALSE, 5, FALSE, MULTI_HEAL_PARTY, NULL)
    return
}

script Basinville_StartingHouse_Shiori_StartingOut {
    setspeakername(Basinville_StartingHouse_Text_ShioriName)
    msgbox(Basinville_StartingHouse_Text_ShioriSpeechStart)
    hidespeakername
    setflag(FLAG_SYS_B_DASH)
    givemon(SPECIES_CHIBI_STAR, 10, ITEM_GEYSER_WATER)
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_SYS_POKEDEX_GET)
    setspeakername(Basinville_StartingHouse_Text_ShioriName)
    msgbox(Basinville_StartingHouse_Text_ShioriSpeechStart2)
    hidespeakername
    return
}

script Basinville_StartingHouse_Shiori_HealParty {
    msgbox(Basinville_StartingHouse_Text_ShioriHeal)
    closemessage
    fadescreen(FADE_TO_BLACK)
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)
    callnative(UpdateFollowingPokemon)
    fadescreen(FADE_FROM_BLACK)
    setspeakername(Basinville_StartingHouse_Text_ShioriName)
    msgbox(Basinville_StartingHouse_Text_ShioriHeal2)
    return
}

script Basinville_StartingHouse_Shiori_WaterShop {
    msgbox(Basinville_StartingHouse_Text_ShioriShop)
    buyonlymart(Basinville_StartingHouse_Shiori_WaterShopItems)
    return
}

script Basinville_StartingHouse_Shiori_SellItems {
    msgbox(Basinville_StartingHouse_Text_ShioriSell)
    itemseller
    return
}

script Basinville_StartingHouse_Shiori_Tangent {
    msgbox(Basinville_StartingHouse_Text_ShioriTangent)
    return
}

mart Basinville_StartingHouse_Shiori_WaterShopItems {
    ITEM_GEYSER_WATER
}

text Basinville_StartingHouse_Text_ShioriName {
    "Shiori"
}

text Basinville_StartingHouse_Text_HealParty {
    "Heal Party"
}

text Basinville_StartingHouse_Text_WaterShop {
    "Water Shop"
}

text Basinville_StartingHouse_Text_SellItems {
    "Sell Items"
}

text Basinville_StartingHouse_Text_Tangent {
    "Tangent"
}

text Basinville_StartingHouse_Text_Nothing {
    "Nothing"
}

text Basinville_StartingHouse_Text_ShioriSpeechStart {
    format("Heya! {PLAYER}, was it? Well, welcome to the new village that is Basinville.\p
    Probably a new sight for you, huh?\p
    Well, as it turns out, my friends Bae and Cecilia wanted to go here to settle which was better, coffee or tea.\p
    I brought you here just in case as backup, in case things go wrong.\p
    Either way, this village does seem to live with these things called Puppets, and they were kind enough to give us one for each of us.\p
    Here, I have this Puppet for you, as well as what seems to be a Dex for archiving what you find.")
}

text Basinville_StartingHouse_Text_YouGotStuff {
    format("You got ")
}

text Basinville_StartingHouse_Text_ShioriSpeechStart2 {
    format("Oh yeah, your Puppets can follow you as well.\p
    It just seems a bit cute seeing your little fellas following you along with this little village here.\p
    By the way, Bae and Cecelia brought some of their gang as well to help out, even setting down shops for the villagers here.\p
    Me personally, I only care about my water here, and you should too.\p
    You should let the villagers know what you want to drink around here. Maybe they can help you out with stuff around here.\p
    I'll be setting down a shop soon here in case you want some good old water to drink.")
}

text Basinville_StartingHouse_Text_ShioriRegular {
    format("Heya, anything you want me to do right now?")
}

text Basinville_StartingHouse_Text_ShioriRegularAgain {
    format("Alright, anything else you want me to do right now?")
}

text Basinville_StartingHouse_Text_ShioriRegular2 {
    format("Have a nice day in the village then.")
}

text Basinville_StartingHouse_Text_ShioriHeal {
    format("Sure, let me just help your party out then.")
}

text Basinville_StartingHouse_Text_ShioriHeal2 {
    format("There, your Puppets should be all better now.")
}

text Basinville_StartingHouse_Text_ShioriShop {
    format("Well, my waters are here if you wanna buy any.")
}

text Basinville_StartingHouse_Text_ShioriSell {
    format("Alright, let me see what you have to offer then.")
}

text Basinville_StartingHouse_Text_ShioriTangent {
    format("Yap yap yap.")
}
